// $Id: 53-insertionsort.oc,v 1.13 2018-04-06 13:20:02-07 - - $
//
// Use insertion sort to print argv in sorted order.
//

#include "oclib.oh"

int strcmp (string s1, string s2) {
   int index = 0;
   bool contin = true;
   while (contin) {
      char s1c = s1[index];
      char s2c = s2[index];
      int cmp = s1c - s2c;
      if (cmp != 0) return cmp;
      if (s1c == '\0') contin = false;
      index = index + 1;
   }
   return 0;
}

void insertion_sort (int size, string[] array) {
   int sorted = 1;
   while (sorted < size) {
      int slot = sorted;
      string element = array[slot];
      bool contin = true;
      while (contin) {
         if (slot == 0) {
            contin = false;
         }else if (strcmp (array[slot - 1], element) <= 0) {
            contin = false;
         }else {
            array[slot] = array[slot - 1];
            slot = slot - 1;
         }
      }
      array[slot] = element;
      sorted = sorted + 1;
   }
}

void print_array (string label, int size, string[] array) {
   putchar ('\n');
   putstr (label);
   putstr (":\n");
   int index = 0;
   while (index < size) {
      putstr (array[index]);
      putchar ('\n');
      index = index + 1;
   }
}

int main (int argc, string[] argv) {
   print_array ("unsorted", argc, argv);
   insertion_sort (argc, argv);
   print_array ("sorted", argc, argv);
}

